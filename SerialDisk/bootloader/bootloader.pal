/TOGGLE-IN (OR RIM/BIN IN) BOOTLOADER
/LOAD AT 0017
/START SERVER ON PC
/CLEAR/CONTINUE AT 0020

BASER=6400
BASET=6410
/BASER=6030
/BASET=6040

SKCF=BASER
SKSF=BASER+1
SKCC=BASER+2
SKRS=BASER+4
SKIE=BASER+5
SKRB=BASER+6

STFL=BASET
STSF=BASET+1
STCF=BASET+2
STPC=BASET+4
STSK=BASET+5
STLS=BASET+6

BOOT=0000

	*17
ADDR,	17
	ZBLOCK 20-.
	CLA CMA		/AC=7777
	DCA ADDR	/SET UP ADDRESS
	TAD BOOTMS	/SEND BOOT MESSAGE TO SERVER
	STLS
	STSF
	JMP .-1
RDLP,	SKCC		/CLEAR AC AND FLAG
	SKSF		/SKIP IF FLAG SET
	JMP .-1
	SKRB		/READ BUFFER
	CLL RTL		/ROTATE 6 LEFT
	RTL
	SPA		/CHANNEL 8 PUNCHED?
	JMP BOOT	/YES, CONTINUE BOOTING
	RTL
	SKSF		/SKIP IF FLAG SET
	JMP .-1
	SKRS		/OR BUFFER WITH AC
	DCA I ADDR	/STORE CONTENTS
	JMP RDLP	/KEEP LOOPING
B7605,	7605		/ADDRESS OF OS/8 ENTRY
BOOTMS,	100		/"@" COMMAND TO SERVER
$
